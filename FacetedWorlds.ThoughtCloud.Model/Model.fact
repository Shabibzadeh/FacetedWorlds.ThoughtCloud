namespace FacetedWorlds.ThoughtCloud.Model;

fact Identity {
key:
    string anonymousId;

query:
    DisableToastNotification* isToastNotificationDisabled {
        DisableToastNotification d : d.identity = this
            where not d.isReenabled
    }

	Cloud* clouds {
		Cloud c : c.creator = this
	}

	Cloud* sharedClouds {
		Share s : s.recipient = this
		Cloud c : s.cloud = c
	}
}

fact Cloud {
key:
	unique;
	Identity creator;

mutable:
	publish Thought centralThought;
}

fact Share {
key:
	publish Identity recipient;
	Cloud cloud;
}

fact Thought {
key:
	unique;
	publish Cloud cloud;

mutable:
	string text;

query:
	Thought* neighbors {
		Link l : l.thoughts = this
		Thought t : l.thoughts = t
	}

	Link* links {
		Link l : l.thoughts = this
	}
}

fact Link {
key:
	Thought* thoughts;
}

fact DisableToastNotification {
key:
    unique;
    Identity identity;

query:
    bool isReenabled {
        exists EnableToastNotification e : e.disable = this
    }
}

fact EnableToastNotification {
key:
    DisableToastNotification* disable;
}
